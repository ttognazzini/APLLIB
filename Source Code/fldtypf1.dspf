     A*%%TS  SD  20230501  154427  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A* File Master List
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/APLDCT)
     A                                      CHGINPDFT(CS UL)
     A                                      ALTHELP(CA01)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A*------------------------------------------------------------------------
     A          R SFL                       SFL
     A*%%TS  SD  20230501  154427  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A            SEL@           1A  P
     A            KEY       R        H      REFFLD(FLDTYPIDN)
     A            SEL       R        B 10  2REFFLD(OPTION)
     A                                      DSPATR(&SEL@)
     A            FLDTYP    R        O 10  5
     A            DES       R   50   O 10 20
     A            NMR       R        O 10 75
     A            ALWDEC    R        O 10 84
     A            REQLEN    R        O 10 93
     A            ALWALC    R        O 10105
     A            FRCLEN    R        O 10115
     A            ACVDES    R        O 10122
     A*------------------------------------------------------------------------
     A          R SFLCTL                    SFLCTL(SFL)
     A*%%TS  SD  20230501  154427  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A                                      SFLPAG(0016)
     A                                      PAGEUP
     A                                      PAGEDOWN
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      CF24
     A                                      RTNCSRLOC(*WINDOW &CSRROW &CSRCOL)
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD &CSRPOS)
     A                                      CSRLOC(OUTROW     OUTCOL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN1)
     A  51                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A N98                                  SFLEND
     A                                      SFLSIZ(&SFLSIZ)
     A            OUTRRN1        4S 0H      SFLRCDNBR
     A            OUTROW         3S 0H
     A            OUTCOL         3S 0H
     A            CSRRRN1        5S 0H
     A            CSRRCD        10A  H
     A            CSRFLD        10A  H
     A            CSRPOS         4S 0H
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
     A            SFLSIZ         5S 0P
     A            SFLMODE        1A  H
     A            FLTEFF         1A  H
     A            SEL1@          1A  P
     A            FLDTYP1@       1A  P
     A            DES1@          1A  P
     A            NMR1@          1A  P
     A            ALWDEC1@       1A  P
     A            ALWALC1@       1A  P
     A            FRCLEN1@       1A  P
     A            ACVDES1@       1A  P
     A            REQLEN1@       1A  P
     A                                  1  2DATE
     A                                      EDTWRD('0  /  /  ')
     A                                  1 11TIME
     A                                      EDTWRD(' 0:  :  ')
     A            ENVIRONMNT    40A  O  1 47MSGID(AAA 0000 ENVIRONMNT)
     A            PGMNME    R        O  1122
     A            MDE       R        O  2  2
     A                                  2 59'Field Type List'
     A                                      COLOR(WHT)
     A                                  2122USER
     A                                  4  1'Search:'
     A            SCHVAL    R        B  4  9
     A            OPTIONS      131A  O  6  1COLOR(BLU)
     A                                  7 75'Is'
     A                                      DSPATR(HI)
     A                                  7 82'Allow'
     A                                      DSPATR(HI)
     A                                  7 91'Require'
     A                                      DSPATR(HI)
     A                                  7103'Allow'
     A                                      DSPATR(HI)
     A                                  7113'Force'
     A                                      DSPATR(HI)
     A                                  8  1'Sel'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  8  5'Type      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 20'Description                       -
     A                                                      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 72'Numeric'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 81'Decimals'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8 91'Length'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8100'Allocation'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8113'Length'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  8122'Active  '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SEL1      R        B  9  2REFFLD(OPTION)
     A                                      DSPATR(&SEL1@)
     A            FLDTYP1   R        B  9  5REFFLD(FLDTYP)
     A                                      DSPATR(&FLDTYP1@)
     A            DES1      R   50   B  9 20REFFLD(DES)
     A                                      DSPATR(&DES1@)
     A                                      CHECK(LC)
     A            NMR1      R        B  9 75REFFLD(NMR)
     A                                      DSPATR(&NMR1@)
     A            ALWDEC1   R        B  9 84REFFLD(ALWDEC)
     A                                      DSPATR(&ALWDEC1@)
     A            REQLEN1   R        B  9 93REFFLD(REQLEN)
     A                                      DSPATR(&REQLEN1@)
     A            ALWALC1   R        B  9105REFFLD(ALWALC)
     A                                      DSPATR(&ALWALC1@)
     A            FRCLEN1   R        B  9115REFFLD(FRCLEN)
     A                                      DSPATR(&FRCLEN1@)
     A            ACVDES1   R        B  9122REFFLD(ACVDES)
     A                                      DSPATR(&ACVDES1@)
     A*
     A*------------------------------------------------------------------------
     A          R FOOTER
     A*%%TS  SD  20230327  103758  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A                                      OVERLAY
     A            FNCKEYS      131A  O 26  2COLOR(BLU)
      *
      *------------------------------------------------------------------------
     A          R MSGRCD                    TEXT('MSG SFL RECORD')
     A                                      SFL SFLMSGRCD(27)
     A            MSGKEY                    SFLMSGKEY
     A            CALLSTK                   SFLPGMQ
      *
      *------------------------------------------------------------------------
     A          R MSGCTL                    TEXT('MSG SFL CONTROL')
     A                                      SFLCTL(MSGRCD) SFLSIZ(10)
     A                                      SFLPAG(1) SFLDSPCTL SFLDSP SFLINZ
     A N99                                  SFLEND
     A            CALLSTK                   SFLPGMQ
