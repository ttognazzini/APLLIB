/* Test monitoring a message queue and emailing some messages */
PGM PARM(&MSGQ &EMAILTO &JOBN &JOBD &USER &DEBUG)
  /* INPUT PARAMETERS */
  DCL &MSGQ    *CHAR LEN(20)
  DCL   &MSGQN *CHAR LEN(10) STG(*DEFINED) DEFVAR(&MSGQ 1)
  DCL   &MSGQL *CHAR LEN(10) STG(*DEFINED) DEFVAR(&MSGQ 11)
  DCL &EMAILTO  *CHAR LEN(50)
  DCL &JOBN    *CHAR LEN(10)
  DCL &JOBD    *CHAR LEN(20)
  DCL   &JOBDN *CHAR LEN(10) STG(*DEFINED) DEFVAR(&JOBD 1)
  DCL   &JOBDL *CHAR LEN(10) STG(*DEFINED) DEFVAR(&JOBD 11)
  DCL &USER    *CHAR LEN(10)
  DCL &DEBUG   *CHAR LEN(4)

  DCL &EMAILPAS  *CHAR LEN(51)

  /* USER FOR ERROR HANDLING */
  DCL &MSGDTA  *CHAR LEN(2000)
  DCL &MSGID   *CHAR LEN(7)
  DCL &MSGF    *CHAR LEN(10)
  DCL &MSGFLIB *CHAR LEN(10)

  /* ON ERROR SEND MESSAGE AND LEAVE */
  MONMSG MSGID(CPC0000 CPD0000 CPF0000 CPI0000 MCH0000) +
         EXEC(GOTO CMDLBL(ERROR))

  /* FINDS THE REAL LIBRARY IF *LIBL OR *CURLIB IS USED, ALSO ENSURES THE +
  /* MESSAGE QUEUE EXISTS */
  RTVOBJD OBJ(&MSGQL/&MSGQN) OBJTYPE(*MSGQ) RTNLIB(&MSGQL)

  /* IF THE JOB NAME IS *MSGQ, CHANGE IT TO THE MESSAGE QUEUE NAME */
  IF (&JOBN = '*MSGQ') (CHGVAR &JOBN &MSGQN)

  /* PARAMTERE OVER 32 CAHRACTERS MOST BE PADDED */
  CHGVAR &EMAILPAS (&EMAILTO *CAT '*')

  /* SUBMITS THE MONITORING JOB */
  IF (&JOBDN = '*USRPRF') +
    SBMJOB  CMD(CALL APLLIB/MONMSQB1 PARM((&MSGQL) (&MSGQN) (&EMAILPAS) (&DEBUG))) +
            JOB(&JOBN) USER(&USER)
  ELSE +
    SBMJOB  CMD(CALL APLLIB/MONMSQB1 PARM((&MSGQL) (&MSGQN) (&EMAILPAS) (&DEBUG))) +
            JOB(&JOBN) JOBD(&JOBDL/&JOBDN) USER(&USER)

  /* ERROR HANDLING */
  ERROR:
    RCVMSG     RMV(*YES) MSGDTA(&MSGDTA) MSGID(&MSGID) +
               MSGF(&MSGF) MSGFLIB(&MSGFLIB)
    SNDPGMMSG  MSGID(&MSGID) MSGF(&MSGFLIB/&MSGF) +
                             MSGDTA(&MSGDTA)

END:        ENDPGM
