/* BUILD CHIMP CUSTOMER CROSS REFERENCE */
PGM (&SPLF1 &QJOB1 &SPLFNO1 &SPLF2 &QJOB2 &SPLFNO2 &CMPTYPE &RPTTYPE +
     &OUTPUT &OUTFILE &OUTMBR)

   /* INPUT VARIABLES FROM THE COMMAND */
   DCL &SPLF1   *CHAR 10
   DCL &QJOB1   *CHAR 26
   DCL   &JOB1  *CHAR STG(*DEFINED) LEN(10) DEFVAR(&QJOB1 1)
   DCL   &USER1 *CHAR STG(*DEFINED) LEN(10) DEFVAR(&QJOB1 11)
   DCL   &NBR1  *CHAR STG(*DEFINED) LEN(6)  DEFVAR(&QJOB1 21)
   DCL &SPLFNO1 *CHAR 11
   DCL &SPLF2   *CHAR 10
   DCL &QJOB2   *CHAR 26
   DCL   &JOB2  *CHAR STG(*DEFINED) LEN(10) DEFVAR(&QJOB2 1)
   DCL   &USER2 *CHAR STG(*DEFINED) LEN(10) DEFVAR(&QJOB2 11)
   DCL   &NBR2  *CHAR STG(*DEFINED) LEN(6)  DEFVAR(&QJOB2 21)
   DCL &SPLFNO2 *CHAR 11
   DCL &CMPTYPE *CHAR 5
   DCL &RPTTYPE *CHAR 8
   DCL &OUTPUT  *CHAR 8
   DCL &OUTFILE *CHAR 20
   DCL   &OFILE *CHAR STG(*DEFINED) LEN(10) DEFVAR(&OUTFILE 1)
   DCL   &OLIB  *CHAR STG(*DEFINED) LEN(10) DEFVAR(&OUTFILE 11)
   DCL &OUTMBR  *CHAR LEN(10)
   DCL &RPLADD  *CHAR LEN(8)

   /* CREATE PHYSICAL FILE FOR BOTH SPOOL FILES */
   DLTF QTEMP/CMPSPLF1
   MONMSG CPF0000
   DLTF QTEMP/CMPSPLF2
   MONMSG CPF0000
   CRTPF FILE(QTEMP/CMPSPLF1) RCDLEN(300) SIZE(*NOMAX) +
      TEXT('CMPSPLF TEMP FILE 1')
   CRTPF FILE(QTEMP/CMPSPLF2) RCDLEN(300) SIZE(*NOMAX) +
      TEXT('CMPSPLF TEMP FILE 2')

   /* COPY SPLF'S INTO THE PHYSICAL FILES */
   IF (&QJOB1 *EQ '*') (CPYSPLF FILE(&SPLF1) TOFILE(QTEMP/CMPSPLF1) SPLNBR(&SPLFNO1))
   ELSE (CPYSPLF FILE(&SPLF1) TOFILE(QTEMP/CMPSPLF1) +
                 JOB(&NBR1/&USER1/&JOB1) SPLNBR(&SPLFNO1))
   IF (&QJOB2 *EQ '*') (CPYSPLF FILE(&SPLF2) TOFILE(QTEMP/CMPSPLF2) SPLNBR(&SPLFNO2))
   ELSE (CPYSPLF FILE(&SPLF2) TOFILE(QTEMP/CMPSPLF2) +
                 JOB(&NBR2/&USER2/&JOB2) SPLNBR(&SPLFNO2))

   /* RUN THE COMPARE COMMAND */
   IF (&OUTPUT = '*OUTFILE') ( +
     CMPPFM NEWFILE(QTEMP/CMPSPLF2) OLDFILE(QTEMP/CMPSPLF1) +
            CMPTYPE(&CMPTYPE) RPTTYPE(&RPTTYPE) +
            OUTPUT(&OUTPUT) OUTFILE(&OLIB/&OFILE) OUTMBR(&OUTMBR &RPLADD) )
   ELSE ( +
     CMPPFM NEWFILE(QTEMP/CMPSPLF2) OLDFILE(QTEMP/CMPSPLF1) +
            CMPTYPE(&CMPTYPE) RPTTYPE(&RPTTYPE) OUTPUT(&OUTPUT) )

ENDPGM
