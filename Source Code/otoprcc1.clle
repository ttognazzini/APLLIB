/* PRINT G/L TRANSACTION LIST */
PGM  (&OTO)
  DCLPRCOPT  DFTACTGRP(*NO) ACTGRP(*CALLER)

  /* output options data structure, master in source member OTOSRVC1PR  */
  DCL &OTO *CHAR 4096
  DCL   &outMdl   *CHAR   2 STG(*DEFINED) DEFVAR(&OTO    1 ) /* Module */
  DCL   &pgmNme   *CHAR  10 STG(*DEFINED) DEFVAR(&OTO    3 ) /* Program Name */
  DCL   &prtOut   *CHAR   1 STG(*DEFINED) DEFVAR(&OTO   13 ) /* Print output (Y/N) */
  DCL   &emlOut   *CHAR   1 STG(*DEFINED) DEFVAR(&OTO   14 ) /* Email output (Y/N) */
  DCL   &arcOut   *CHAR   1 STG(*DEFINED) DEFVAR(&OTO   15 ) /* Archie ouput (Y/N) */
  DCL   &faxOut   *CHAR   1 STG(*DEFINED) DEFVAR(&OTO   16 ) /* Fax Output */
  DCL   &pstFlg   *CHAR   1 STG(*DEFINED) DEFVAR(&OTO   17 ) /* post (Y/N) */
  DCL   &prtDev   *CHAR  10 STG(*DEFINED) DEFVAR(&OTO   18 ) /* Printer ID */
  DCL   &prtOtq   *CHAR  10 STG(*DEFINED) DEFVAR(&OTO   28 ) /* ouptut queue */
  DCL   &hldOut   *CHAR   5 STG(*DEFINED) DEFVAR(&OTO   38 ) /* hold (*YES/*NO) */
  DCL   &savOut   *CHAR   5 STG(*DEFINED) DEFVAR(&OTO   43 ) /* save output (*YES/*NO) */
  DCL   &prtFrm   *CHAR  10 STG(*DEFINED) DEFVAR(&OTO   48 ) /* form */
  DCL   &usrDta   *CHAR  10 STG(*DEFINED) DEFVAR(&OTO   58 ) /* user data */
  DCL   &nbrCpy   *CHAR   3 STG(*DEFINED) DEFVAR(&OTO   68 ) /* copies */
  DCL   &prtQul   *CHAR  10 STG(*DEFINED) DEFVAR(&OTO   71 ) /* print quality */
  DCL   &prtOvl   *CHAR  10 STG(*DEFINED) DEFVAR(&OTO   81 ) /* overlay, used on chgprtf */
  DCL   &arcFlr   *CHAR 128 STG(*DEFINED) DEFVAR(&OTO   91 ) /* Archive File name */
  DCL   &aarFlr   *CHAR 128 STG(*DEFINED) DEFVAR(&OTO  219 ) /* Auto Archive folder */
  DCL   &faxNbr   *CHAR  20 STG(*DEFINED) DEFVAR(&OTO  347 ) /* fax number */
  DCL   &emlAdd   *CHAR  60 STG(*DEFINED) DEFVAR(&OTO  367 ) /* Email address */
  DCL   &emlNme   *CHAR  30 STG(*DEFINED) DEFVAR(&OTO  427 ) /* Email Name */
  DCL   &frmEml   *CHAR  60 STG(*DEFINED) DEFVAR(&OTO  457 ) /* from address */
  DCL   &frmNme   *CHAR  30 STG(*DEFINED) DEFVAR(&OTO  517 ) /* from name */
  DCL   &cc1Eml   *CHAR  60 STG(*DEFINED) DEFVAR(&OTO  547 ) /* cc address 1 */
  DCL   &cc1Nme   *CHAR  30 STG(*DEFINED) DEFVAR(&OTO  607 ) /* cc name 1 */
  DCL   &cc2Eml   *CHAR  60 STG(*DEFINED) DEFVAR(&OTO  637 ) /* cc address 2 */
  DCL   &cc2Nme   *CHAR  30 STG(*DEFINED) DEFVAR(&OTO  697 ) /* cc name 2 */
  DCL   &cc3Eml   *CHAR  60 STG(*DEFINED) DEFVAR(&OTO  727 ) /* cc address 3 */
  DCL   &cc3Nme   *CHAR  30 STG(*DEFINED) DEFVAR(&OTO  787 ) /* cc name 3 */
  DCL   &bccEml   *CHAR  60 STG(*DEFINED) DEFVAR(&OTO  817 ) /* bcc address */
  DCL   &bccNme   *CHAR  30 STG(*DEFINED) DEFVAR(&OTO  877 ) /* bcc name */
  DCL   &emlSbj   *CHAR  60 STG(*DEFINED) DEFVAR(&OTO  907 ) /* subject */
  DCL   &ataTyp   *CHAR   5 STG(*DEFINED) DEFVAR(&OTO  967 ) /* Attachment type */
  DCL   &vldAta   *CHAR   1 STG(*DEFINED) DEFVAR(&OTO  972 ) /* Vld Att Typ,=PDF,TIFF;2=+CSV,3=+XML,4=+XLS*/
  DCL   &ataNme   *CHAR 128 STG(*DEFINED) DEFVAR(&OTO  973 ) /* File Name */
  DCL   &ataFmt   *CHAR   1 STG(*DEFINED) DEFVAR(&OTO 1101 ) /* CSV File format (1=Data,2=Report) */
  DCL   &crtXma   *CHAR   1 STG(*DEFINED) DEFVAR(&OTO 1102 ) /* Create XML attribute file (Y/N) */
  DCL   &emlMsg01 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1103 ) /* Message 1  */
  DCL   &emlMsg02 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1178 ) /* Message 2  */
  DCL   &emlMsg03 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1253 ) /* Message 3  */
  DCL   &emlMsg04 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1328 ) /* Message 4  */
  DCL   &emlMsg05 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1403 ) /* Message 5  */
  DCL   &emlMsg06 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1478 ) /* Message 6  */
  DCL   &emlMsg07 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1553 ) /* Message 7  */
  DCL   &emlMsg08 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1628 ) /* Message 8  */
  DCL   &emlMsg09 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1703 ) /* Message 9  */
  DCL   &emlMsg10 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1778 ) /* Message 10 */
  DCL   &emlMsg11 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1853 ) /* Message 11 */
  DCL   &emlMsg12 *CHAR  75 STG(*DEFINED) DEFVAR(&OTO 1928 ) /* Message 12 */
  DCL   &pstFlr   *CHAR 128 STG(*DEFINED) DEFVAR(&OTO 2003 ) /* System Post Folder */
  DCL   &prtFle   *CHAR  10 STG(*DEFINED) DEFVAR(&OTO 2131 ) /* Print file name */
  DCL   &pgeWid   *CHAR   3 STG(*DEFINED) DEFVAR(&OTO 2141 ) /* Print file page width */
  DCL   &pgeLen   *CHAR   3 STG(*DEFINED) DEFVAR(&OTO 2144 ) /* Print file page length */
  DCL   &endChr   *CHAR   1 STG(*DEFINED) DEFVAR(&OTO 4096 ) /* Populated with an astrisk */

  /* Data structure for API call to QSPRILSP, get jobs last SPLF created */
  DCL &RCVVAR *CHAR 70
  DCL   &BYTESAVAIL *INT   4 STG(*DEFINED) DEFVAR(&RCVVAR  1)
  DCL   &BYTESRTN   *INT   4 STG(*DEFINED) DEFVAR(&RCVVAR  5)
  DCL   &SPLFNAME   *CHAR 10 STG(*DEFINED) DEFVAR(&RCVVAR  9)
  DCL   &JOBNAME    *CHAR 10 STG(*DEFINED) DEFVAR(&RCVVAR 19)
  DCL   &USERNAME   *CHAR 10 STG(*DEFINED) DEFVAR(&RCVVAR 29)
  DCL   &JOBNBR     *CHAR  6 STG(*DEFINED) DEFVAR(&RCVVAR 39)
  DCL   &SPLFNBRINT *INT   4 STG(*DEFINED) DEFVAR(&RCVVAR 45)
  DCL   &SYSNAME    *CHAR  8 STG(*DEFINED) DEFVAR(&RCVVAR 49)
  DCL   &SPLFCRTDAT *CHAR  7 STG(*DEFINED) DEFVAR(&RCVVAR 57)
  DCL   &SPLFCRTTIM *CHAR  6 STG(*DEFINED) DEFVAR(&RCVVAR 65)

  /* variables used for call to QSPRILSP, get jobs last SPLF created */
  DCL &RCVVARLEN  *INT  4  VALUE(70)
  DCL &FMTNAME    *CHAR 10
  DCL &ERRORCODE  *CHAR 8
  DCL &SPLFEXISTS *LGL
  DCL &SPLFNBR    *char 6

  DCL &PATH    *CHAR 266
  DCL &SPLJOB  *CHAR 26
  DCL &EOR     *CHAR 2 Value(x'0D25')
  DCL &MSG     *CHAR 922


  /* GET LAST SPLF CREATED BY THIS JOB */
  CHGVAR &BYTESAVAIL 70
  CHGVAR &FMTNAME    'SPRL0100'
  CHGVAR &ERRORCODE  X'0000000000000000'
  CALL   QSPRILSP    (&RCVVAR &RCVVARLEN &FMTNAME &ERRORCODE)
  MONMSG CPF333A     EXEC(CHGVAR VAR(&SPLFEXISTS) VALUE('0'))
  CHGVAR &SPLFNBR (%char(&SPLFNBRINT))

  /* IF THE LAST SPLF CREATED IS NOT THE SAME NAME USE CURRENT JOB */
  IF (&SPLFNAME *NE &PRTFLE) (DO)
    CHGVAR &SPLFNAME &PRTFLE
    RTVJOBA &JOBNAME &USERNAME &JOBNBR
    CHGVAR &SPLFNBR '*LAST'
  ENDDO

  CHGVAR &SPLJOB (&JOBNAME *CAT &USERNAME *CAT &JOBNBR)

  /* THIS PROGRAM JUST FIXES ANY INVALID CHARACTERS IN THE FILE NAME */
  CALL OTOPRCB2 &OTO

  /* IF PRINT CHANGE SPOOLED FILE */
  IF (&PRTOUT = 'Y') (DO)
    /* LOAD DEFAULTS FOR EMPTY VALUES */
    IF (&NBRCPY *EQ '   ') (CHGVAR &NBRCPY '1')
    IF (&PRTDEV *EQ '   ') (CHGVAR &PRTDEV '*SAME')
    IF (&PRTFRM *EQ '   ') (CHGVAR &PRTFRM '*SAME')
    IF (&PRTOTQ *EQ '   ') (CHGVAR &PRTOTQ '*SAME')
    IF (&SAVOUT *EQ '   ') (CHGVAR &SAVOUT '*SAME')
    IF (&HLDOUT *EQ '   ') (CHGVAR &HLDOUT '*SAME')
    IF (&USRDTA *EQ '   ') (CHGVAR &USRDTA '*SAME')
    IF (&PRTQUL *EQ '   ') (CHGVAR &PRTQUL '*SAME')
    /* CHANGE ATRIBUTES */
    CHGSPLFA &PRTFLE &JOBNBR/&USERNAME/&JOBNAME &SPLFNBR +
             FORMTYPE(&PRTFRM) COPIES(&NBRCPY) SAVE(&SAVOUT) USRDTA(&USRDTA) PRTQLTY(&PRTQUL)
    MONMSG   MSGID(CPF0000)
    CHGSPLFA &PRTFLE &JOBNBR/&USERNAME/&JOBNAME &SPLFNBR DEV(&PRTDEV)
    MONMSG   MSGID(CPF0000)
    CHGSPLFA &PRTFLE &JOBNBR/&USERNAME/&JOBNAME &SPLFNBR OUTQ(&PRTOTQ)
    MONMSG   MSGID(CPF0000)
  ENDDO


  /* IF EMAIL CALL EMAIL PROGRAM */
  IF (&EMLOUT = 'Y') (DO)
    /* CREATE MESSAGE STRING */
    CHGVAR &MSG (&EMLMSG01 *TCAT &EOR *TCAT &EMLMSG02 *TCAT &EOR +
            *TCAT &EMLMSG03 *TCAT &EOR *TCAT &EMLMSG04 *TCAT &EOR +
            *TCAT &EMLMSG05 *TCAT &EOR *TCAT &EMLMSG06 *TCAT &EOR +
            *TCAT &EMLMSG07 *TCAT &EOR *TCAT &EMLMSG08 *TCAT &EOR +
            *TCAT &EMLMSG09 *TCAT &EOR *TCAT &EMLMSG10 *TCAT &EOR +
            *TCAT &EMLMSG11 *TCAT &EOR *TCAT &EMLMSG12)

    /* Get the path to the file or create for *TXT and *PDF */
    IF (&ATATYP = '*CSV') (DO)
      CHGVAR &PATH   ('/EMAIL/TMP/' *CAT &ATANME *TCAT '.csv')
    ENDDO
    ELSE IF (&ATATYP = '*XML') (DO)
      CHGVAR &PATH   ('/EMAIL/TMP/' *CAT &ATANME *TCAT '.xml')
    ENDDO
    ELSE IF (&ATATYP = '*XLSX') (DO)
      CHGVAR &PATH   ('/EMAIL/TMP/' *CAT &ATANME *TCAT '.xlsx')
    ENDDO
    ELSE IF (&ATATYP = '*TEXT') (DO)
        CHGVAR &PATH   ('/EMAIL/TMP/' *CAT &ATANME *TCAT '.txt')
        CALLSUBR CRTTXTFILE
    ENDDO
    ELSE (DO)
      CHGVAR  &PATH ('/EMAIL/TMP/' *CAT &ATANME *TCAT '.pdf')
      CPYSPLF &PRTFLE *TOSTMF JOB(&JOBNBR/&USERNAME/&JOBNAME) SPLNBR(&SPLFNBR) TOSTMF(&PATH) WSCST(*PDF)
    ENDDO

    /* SEND EMAIL WITH CONVERTED SPLF AS ATTACHEMNT */
    /* TODO FIGURE OUT HOW TO ADD NAMES TO THE EMAIL ADDRESSES */
    IF (&CC1EML *NE ' ' & &CC2EML *NE ' ' & &CC3EML *NE ' ') ( +
          RJSSMTP/SMTPSEND TOADDR(&EMLADD &CC1EML &CC2EML &CC3EML) +
                           FROMADDR(&FRMEML) SUBJECT(&EMLSBJ) +
                           MESSAGE(&MSG) ATTACHMENT(&PATH) +
                           CONTYPE('text/html'))
    ELSE IF (&CC1EML *NE ' ' & &CC2EML *NE ' ') ( +
          RJSSMTP/SMTPSEND TOADDR(&EMLADD &CC1EML &CC3EML) +
                           FROMADDR(&FRMEML) SUBJECT(&EMLSBJ) +
                           MESSAGE(&MSG) ATTACHMENT(&PATH) +
                           CONTYPE('text/html'))
    ELSE IF (&CC1EML *NE ' ') ( +
          RJSSMTP/SMTPSEND TOADDR(&EMLADD &CC1EML) +
                           FROMADDR(&FRMEML) SUBJECT(&EMLSBJ) +
                           MESSAGE(&MSG) ATTACHMENT(&PATH) +
                           CONTYPE('text/html'))
    ELSE ( RJSSMTP/SMTPSEND TOADDR(&EMLADD) +
                           FROMADDR(&FRMEML) SUBJECT(&EMLSBJ) +
                           MESSAGE(&MSG) ATTACHMENT(&PATH) +
                           CONTYPE('text/html'))
    /* Send BCC as a seperate email */
    IF (&BCCEML *NE ' ') ( +
       RJSSMTP/SMTPSEND TOADDR(&BCCEML) FROMADDR(&FRMEML) +
                        SUBJECT(&EMLSBJ) MESSAGE(&MSG) +
                        ATTACHMENT(&PATH) CONTYPE('text/html'))


    /* CLEANUP TEMP FILE */
     DEL OBJLNK(&PATH)
     MONMSG CPF0000
  ENDDO


  /* IF ARCHIVE */
  IF (&ARCOUT = 'Y' *AND &ARCFLR *NE ' ') (DO)
    CALL SYS280 'DDRIVEINT'
    /* USE CSV FILE IF SELECTED, MUST ALREADY BE MADE */
    IF (&ATATYP = '*CSV' *AND &ARCFLR *NE '/EMAIL/TMP') (DO)
      CHGVAR &PATH   (&ARCFLR *TCAT '/' *TCAT &ATANME *TCAT '.csv')
      DEL &PATH
      MONMSG CPF0000
      CHGVAR &PATH   ('/EMAIL/TMP/' *TCAT &ATANME *TCAT '.csv')
      MOV    &PATH    TODIR(&ARCFLR) TOCCSID(*CALC)
    ENDDO
    /* USE XML FILE IF SELECTED, MUST ALREADY BE MADE */
    ELSE IF (&ATATYP = '*XML' *AND &ARCFLR *NE '/EMAIL/TMP') (DO)
      CHGVAR &PATH   (&ARCFLR *TCAT '/' *TCAT &ATANME *TCAT '.xml')
      DEL &PATH
      MONMSG CPF0000
      CHGVAR &PATH   ('/EMAIL/TMP/' *TCAT &ATANME *TCAT '.xml')
      MOV    &PATH    TODIR(&ARCFLR) TOCCSID(*CALC)
    ENDDO
    /* USE XLSX FILE IF SELECTED, MUST ALREADY BE MADE */
    ELSE IF (&ATATYP = '*XLSX' *AND &ARCFLR *NE '/EMAIL/TMP') (DO)
      CHGVAR &PATH   (&ARCFLR *TCAT '/' *TCAT &ATANME *TCAT '.xlsx')
      DEL &PATH
      MONMSG CPF0000
      CHGVAR &PATH   ('/EMAIL/TMP/' *TCAT &ATANME *TCAT '.xlsx')
      MOV    &PATH    TODIR(&ARCFLR) TOCCSID(*CALC)
    ENDDO
    /* CREATE TEXT FILE IF SELECTED */
    ELSE IF (&ATATYP = '*TEXT') (DO)
      CHGVAR &PATH (%trim(&ARCFLR) *CAT '/'  *CAT &ATANME *TCAT '.txt')
      CALLSUBR CRTTXTFILE
    ENDDO
    /* OTHERWISE CREATE PDF FILE */
    ELSE (DO)
      CHGVAR &PATH   (&ARCFLR *CAT &ATANME *TCAT '.pdf')
      CPYSPLF &PRTFLE *TOSTMF JOB(&JOBNBR/&USERNAME/&JOBNAME) SPLNBR(&SPLFNBR) TOSTMF(&PATH) WSCST(*PDF)
    ENDDO
    /* CREATE XML ATTRIBUTE FILE IF SELECTED */
    IF (&CRTXMA = 'Y') (DO)
      CHGVAR &PATH (&ARCFLR *CAT &ATANME *TCAT '.xml')
      CHGVAR &PATH (%SST(&PATH 1 255) *CAT '*')
      CALL OTOPRCB1 (&PATH)
    ENDDO
    CALL SYS280
  ENDDO


  /* FAX IF SELECTED */
  IF (&FAXOUT = 'Y') (DO)
    /* TODO ADD FAX LOGIC HERE IF POSSIBLE */
  ENDDO


  /* IF POST ARCHIVE THE REPORT */
  IF (&PSTFLG = 'Y' *AND &PSTFLR *NE ' ') (DO)
    CALL SYS280 'DDRIVEINT'
    CHGVAR  &PATH (&PSTFLR *TCAT '/' *TCAT &ATANME *TCAT '.pdf')
    CPYSPLF &PRTFLE *TOSTMF JOB(&JOBNBR/&USERNAME/&JOBNAME) SPLNBR(&SPLFNBR) TOSTMF(&PATH) WSCST(*PDF)
    CALL SYS280
  ENDDO


  /* DELETE THE SPOOL FILE IF THE OUTPUT IS NOT SET TO HOLD, PRINT OR FAX */
  IF (&PRTOUT *EQ 'N' & &HLDOUT *NE '*YES' & &FAXOUT *NE 'Y') (DO)
      DLTSPLF &SPLFNAME JOB(&JOBNBR/&USERNAME/&JOBNAME) SPLNBR(&SPLFNBR)
      MONMSG CPF0000
      ENDDO

  /* RELEASE THE SPLF IF THE OUTPUT IS SET TO PRINT AND NOT HOLD */
  IF (&PRTOUT *NE 'N' & &HLDOUT *EQ '*NO') (DO)
      RLSSPLF &SPLFNAME JOB(&JOBNBR/&USERNAME/&JOBNAME) SPLNBR(&SPLFNBR)
      MONMSG CPF0000
      ENDDO


/* SUBROUTINE TO CREATE A TEXT FILE FROM AN SPOOLED FILE */
SUBR CRTTXTFILE

  /* CREATE TEMP FILE*/
  DLTF    QTEMP/OTOTXTF
  MONMSG  CPF0000
  CRTPF   QTEMP/OTOTXTF RCDLEN(255) MBR(OTOTXTR)
  CHGPF   QTEMP/OTOTXTF SIZE(*NOMAX)
  MONMSG  CPF0000

  /* COPY SPOOL FILE TO TEMP FILE */
  CPYSPLF &PRTFLE TOFILE(QTEMP/OTOTXTF) JOB(&JOBNBR/&USERNAME/&JOBNAME) SPLNBR(&SPLFNBR) CTLCHAR(*PRTCTL)

  /* COPY TEMP FILE TO IFS */
  CPYTOSTMF FROMMBR('/QSYS.lib/QTEMP.lib/OTOTXTF.file/OTOTXTR.mbr') +
            TOSTMF(&PATH) +
            STMFOPT(*REPLACE) STMFCCSID(*PCASCII)

  /* REMOVE TEMP FILES */
  DLTF      QTEMP/OTOTXTF
  MONMSG    CPF0000

ENDSUBR

ENDPGM
