     A*%%TS  SD  20230504  135141  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A* File Master List
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4)
     A                                      REF(*LIBL/APLDCT)
     A                                      CHGINPDFT(CS UL)
     A                                      ALTHELP(CA01)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A*------------------------------------------------------------------------
     A          R SFL                       SFL
     A*%%TS  SD  20230504  134806  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A            SEL@           1A  P
     A            KEY       R        H      REFFLD(FLEMSTIDN)
     A            SEL       R        B  9  2REFFLD(OPTION)
     A                                      DSPATR(&SEL@)
     A            FLELIB    R        O  9  5
     A            FLENME    R        O  9 16
     A            FLEDES    R   50   O  9 27
     A            TBLTYP    R        O  9 78
     A            STSDES        11A  O  9 89TEXT('Active Description')
     A                                      ALIAS(ACTIVE_DESCRIPTION)
     A            FLDCNT    R        O  9101EDTCDE(Z)
     A            IDXCNT    R        O  9108EDTCDE(Z)
     A            NTEEXS    R        O  9116
     A            DCTNME    R        O  9119
     A            FLEERR    R        O  9131COLOR(RED)
     A*------------------------------------------------------------------------
     A          R SFLCTL                    SFLCTL(SFL)
     A*%%TS  SD  20230504  135141  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A                                      SFLPAG(0017)
     A                                      PAGEUP
     A                                      PAGEDOWN
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      CF24
     A                                      RTNCSRLOC(*WINDOW &CSRROW &CSRCOL)
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD &CSRPOS)
     A                                      CSRLOC(OUTROW     OUTCOL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN1)
     A  51                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A                                      SFLSIZ(&SFLSIZ)
     A            OUTRRN1        4S 0H      SFLRCDNBR
     A            OUTROW         3S 0H
     A            OUTCOL         3S 0H
     A            CSRRRN1        5S 0H
     A            CSRRCD        10A  H
     A            CSRFLD        10A  H
     A            CSRPOS         4S 0H
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
     A            SFLSIZ         5S 0P
     A            SFLMODE        1A  H
     A            FLTEFF         1A  H
     A            SEL1@          1A  P
     A            FLELIB1@       1A  P
     A            FLEDES1@       1A  P
     A            ACVDES1@       1A  P
     A            FLENME1@       1A  P
     A            FLDCNT1@       1A  P
     A            IDXCNT1@       1A  P
     A            NTEEXS1@       1A  P
     A            TBLTYP1@       1A  P
     A            DCTNME1@       1A  P
     A            FLEERR1@       1A  P
     A                                  1  2DATE
     A                                      EDTWRD('0  /  /  ')
     A                                  1 11TIME
     A                                      EDTWRD(' 0:  :  ')
     A            ENVIRONMNT    40A  O  1 47MSGID(AAA 0000 ENVIRONMNT)
     A            PGMNME    R        O  1122
     A            MDE       R        O  2  2
     A                                  2 59'File Master List'
     A                                      COLOR(WHT)
     A                                  2122USER
     A                                  4  1'Search:'
     A            SCHVAL    R        B  4  9
     A            OPTIONS      131A  O  6  1COLOR(BLU)
     A                                  7  1'Sel'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  5'Library   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 16'File      '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 27'Description                       -
     A                                                      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 78'Type      '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 89'Status     '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7101'Fields'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7108'Indexs'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7115'Nts'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7119'Dictionary'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7130'Err'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SEL1      R        B  8  2REFFLD(OPTION)
     A                                      DSPATR(&SEL1@)
     A            FLELIB1   R        B  8  5REFFLD(FLELIB)
     A                                      DSPATR(&FLELIB1@)
     A            FLENME1   R        B  8 16REFFLD(FLENME)
     A                                      DSPATR(&FLENME1@)
     A            FLEDES1   R   50   B  8 27REFFLD(FLEDES)
     A                                      DSPATR(&FLEDES1@)
     A                                      CHECK(LC)
     A            TBLTYP1   R        B  8 78REFFLD(TBLTYP)
     A                                      DSPATR(&TBLTYP1@)
     A            STSDES1       11A  B  8 89DSPATR(&ACVDES1@)
     A                                      CHECK(LC)
     A                                      TEXT('Active Description')
     A                                      ALIAS(ACTIVE_DESCRIPTION)
     A            FLDCNT1   R        B  8101REFFLD(FLDCNT)
     A                                      DSPATR(&FLDCNT1@)
     A            IDXCNT1   R        B  8108REFFLD(IDXCNT)
     A                                      DSPATR(&IDXCNT1@)
     A            NTEEXS1   R        B  8116REFFLD(NTEEXS)
     A                                      DSPATR(&NTEEXS1@)
     A            DCTNME1   R        B  8119REFFLD(DCTNME)
     A                                      DSPATR(&DCTNME1@)
     A            FLEERR1   R        B  8131REFFLD(FLEERR)
     A                                      DSPATR(&FLEERR1@)
     A*
     A*------------------------------------------------------------------------
     A          R FOOTER
     A*%%TS  SD  20230327  103758  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A                                      OVERLAY
     A            FNCKEYS      131A  O 26  2COLOR(BLU)
      *
      *------------------------------------------------------------------------
     A          R MSGRCD                    TEXT('MSG SFL RECORD')
     A                                      SFL SFLMSGRCD(27)
     A            MSGKEY                    SFLMSGKEY
     A            CALLSTK                   SFLPGMQ
      *
      *------------------------------------------------------------------------
     A          R MSGCTL                    TEXT('MSG SFL CONTROL')
     A                                      SFLCTL(MSGRCD) SFLSIZ(10)
     A                                      SFLPAG(1) SFLDSPCTL SFLDSP SFLINZ
     A N99                                  SFLEND
     A            CALLSTK                   SFLPGMQ
