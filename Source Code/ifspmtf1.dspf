     A*%%TS  SD  20231218  134829  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A* Dictionary Master List
     A*
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/APLDCT)
     A                                      CHGINPDFT(CS UL)
     A                                      ALTHELP(CA01)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A*------------------------------------------------------------------------
     A          R SFL                       SFL
     A*%%TS  SD  20231218  130647  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A            SEL@           1A  P
     A            KEY          999A  H
     A            SEL       R        B 11  2REFFLD(OPTION)
     A                                      DSPATR(&SEL@)
     A            FLE           70A  O 11  5
     A            TYP            5   O 11 76
     A*------------------------------------------------------------------------
     A          R SFLCTL                    SFLCTL(SFL)
     A*%%TS  SD  20231218  134829  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A                                      SFLPAG(0012)
     A                                      PAGEUP
     A                                      PAGEDOWN
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      CF24
     A                                      RTNCSRLOC(*WINDOW &CSRROW &CSRCOL)
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD &CSRPOS)
     A                                      CSRLOC(OUTROW     OUTCOL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN1)
     A  51                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A                                      SFLSIZ(&SFLSIZ)
     A            OUTRRN1        4S 0H      SFLRCDNBR
     A            OUTROW         3S 0H
     A            OUTCOL         3S 0H
     A            CSRRRN1        5S 0H
     A            CSRRCD        10A  H
     A            CSRFLD        10A  H
     A            CSRPOS         4S 0H
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
     A            SFLSIZ         5S 0P
     A            SFLMODE        1A  H
     A            SEL1@          1A  P
     A            FLE1@          1A  P
     A                                  1  2DATE
     A                                      EDTWRD('0  /  /  ')
     A                                  1 11TIME
     A                                      EDTWRD(' 0:  :  ')
     A            ENVIRONMNT    40A  O  1 21MSGID(AAA 0000 ENVIRONMNT)
     A            PGMNME    R        O  1 70
     A            MDE       R        O  2  2
     A                                  2 34'File Selection'
     A                                      COLOR(WHT)
     A                                  2 70USER
     A            OPTIONS       79A  O  8  1COLOR(BLU)
     A                                  6  1'Search:'
     A            SCHVAL    R        B  6  9
     A                                  9  1'Sel'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  9  5'File                              -
     A                                                                         -
     A                                       '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SEL1      R        B 10  2REFFLD(OPTION)
     A                                      DSPATR(&SEL1@)
     A            FLE1          70A  B 10  5DSPATR(&FLE1@)
     A                                      CHECK(LC)
     A                                      TEXT('Description')
     A                                      ALIAS(DESCRIPTION)
     A            TYP1           5A  B 10 76
     A                                  9 76'Type '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  4  1'Folder:'
     A            CURDIRSHT    151A  O  4  9
     A*
     A*------------------------------------------------------------------------
     A          R FOOTER
     A                                      OVERLAY
     A            FNCKEYS       79A  O 23  2COLOR(BLU)
      *
      *------------------------------------------------------------------------
     A          R MSGRCD                    TEXT('MSG SFL RECORD')
     A                                      SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            CALLSTK                   SFLPGMQ
      *
      *------------------------------------------------------------------------
     A          R MSGCTL                    TEXT('MSG SFL CONTROL')
     A                                      SFLCTL(MSGRCD) SFLSIZ(10)
     A                                      SFLPAG(1) SFLDSPCTL SFLDSP SFLINZ
     A N99                                  SFLEND
     A            CALLSTK                   SFLPGMQ
