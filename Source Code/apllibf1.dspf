     A*%%TS  SD  20230331  121824  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A* Dictionary Master List
     A*
     A*%%EC
     A                                      DSPSIZ(27 132 *DS3)
     A                                      REF(*LIBL/APLDCT)
     A                                      CHGINPDFT(CS UL)
     A                                      ALTHELP(CA01)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A*------------------------------------------------------------------------
     A          R SFL                       SFL
     A*%%TS  SD  20230331  121824  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A            SEL@           1A  P
     A            KEY           10A  H
     A            SEL       R        B  9  2REFFLD(OPTION)
     A                                      DSPATR(&SEL@)
     A            LIBNME    R        O  9  5REFFLD(LIBNME)
     A            LIBDES    R        O  9 16REFFLD(LIBDES)
     A            ACVDES    R        O  9 68
     A            LIBTYPD       15A  O  9 78
     A            DEVUSR    R        O  9 95
     A*------------------------------------------------------------------------
     A          R SFLCTL                    SFLCTL(SFL)
     A*%%TS  SD  20230331  121824  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A                                      SFLPAG(0017)
     A                                      PAGEUP
     A                                      PAGEDOWN
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      CF24
     A                                      RTNCSRLOC(*WINDOW &CSRROW &CSRCOL)
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD &CSRPOS)
     A                                      CSRLOC(OUTROW     OUTCOL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN1)
     A  51                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A                                      SFLSIZ(&SFLSIZ)
     A            OUTRRN1        4S 0H      SFLRCDNBR
     A            OUTROW         3S 0H
     A            OUTCOL         3S 0H
     A            CSRRRN1        5S 0H
     A            CSRRCD        10A  H
     A            CSRFLD        10A  H
     A            CSRPOS         4S 0H
     A            CSRROW         3S 0H
     A            CSRCOL         3S 0H
     A            SFLSIZ         5S 0P
     A            SFLMODE        1A  H
     A            FLTEFF         1A  H
     A            SEL1@          1A  P
     A            LIBNME1@       1A  P
     A            LIBDES1@       1A  P
     A            LIBTYPD1@      1A  P
     A            ACVDES1@       1A  P
     A            DEVUSR1@       1A  P
     A                                  1  2DATE
     A                                      EDTWRD('0  /  /  ')
     A                                  1 11TIME
     A                                      EDTWRD(' 0:  :  ')
     A            ENVIRONMNT    40A  O  1 47MSGID(AAA 0000 ENVIRONMNT)
     A            PGMNME    R        O  1122
     A            MDE       R        O  2  2
     A                                  2 58'Fabricut Libraries'
     A                                      COLOR(WHT)
     A                                  2122USER
     A                                  4  1'Search:'
     A            SCHVAL    R        B  4  9
     A            OPTIONS      131A  O  6  1COLOR(BLU)
     A                                  7  1'Sel'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  7  5'Library   '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 16'Description                       -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  7 68'Active  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 78'Library Type   '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A                                  7 95'Dev User  '
     A                                      DSPATR(UL)
     A                                      DSPATR(HI)
     A            SEL1      R        B  8  2REFFLD(OPTION)
     A                                      DSPATR(&SEL1@)
     A            LIBNME1   R        B  8  5REFFLD(LIBNME)
     A                                      DSPATR(&LIBNME1@)
     A            LIBDES1   R        B  8 16REFFLD(LIBDES)
     A                                      DSPATR(&LIBDES1@)
     A                                      CHECK(LC)
     A            ACVDES1   R        B  8 68REFFLD(ACVDES)
     A                                      DSPATR(&ACVDES1@)
     A                                      CHECK(LC)
     A            LIBTYPD1      15A  B  8 78DSPATR(&LIBTYPD1@)
     A            DEVUSR1   R        B  8 95REFFLD(DEVUSR)
     A                                      DSPATR(&DEVUSR1@)
     A*
     A*------------------------------------------------------------------------
     A          R FOOTER
     A*%%TS  SD  20230331  102710  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A                                      OVERLAY
     A            FNCKEYS      131A  O 26  2COLOR(BLU)
      *
      *------------------------------------------------------------------------
     A          R MSGRCD                    TEXT('MSG SFL RECORD')
     A                                      SFL SFLMSGRCD(27)
     A            MSGKEY                    SFLMSGKEY
     A            CALLSTK                   SFLPGMQ
      *
      *------------------------------------------------------------------------
     A          R MSGCTL                    TEXT('MSG SFL CONTROL')
     A                                      SFLCTL(MSGRCD) SFLSIZ(10)
     A                                      SFLPAG(1) SFLDSPCTL SFLDSP SFLINZ
     A N99                                  SFLEND
     A            CALLSTK                   SFLPGMQ
