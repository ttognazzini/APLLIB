     A*%%TS  SD  20250506  100033  TTOGNAZZIN  REL-V7R5M0  5770-WDS
     A* Dictionary Value Prompt
     A*%%EC
     A                                      DSPSIZ(27 132 *DS4                 -
     A                                             24 80 *DS3)
     A                                      REF(*LIBL/APLDCT)
     A                                      CHGINPDFT(CS UL)
     A                                      ALTHELP(CA01)
     A                                      HELP
     A                                      PRINT(*LIBL/QSYSPRT)
     A          R SFL                       SFL
     A*%%TS  SD  20220705  142229  TTOGNAZZIN  REL-V7R4M0  5770-WDS
     A            SEL@           1A  P
     A            KEY           10A  H
     A            SEL       R        B  7  2REFFLD(OPTION)
     A                                      DSPATR(&SEL@)
     A            ACTCDE    R        O  7  5
     A            DES       R        O  7 10
     A            FNCKEY    R        O  7 51
     A          R SFLCTL                    SFLCTL(SFL)
     A*%%TS  SD  20250506  100033  TTOGNAZZIN  REL-V7R5M0  5770-WDS
     A  *DS4                                SFLPAG(0009)
     A  *DS3                                SFLPAG(0009)
     A  *DS4                                WINDOW(FOOTER)
     A  *DS3                                WINDOW(FOOTER)
     A                                      PAGEUP
     A                                      PAGEDOWN
     A                                      CF01
     A                                      CF02
     A                                      CF03
     A                                      CF04
     A                                      CF05
     A                                      CF06
     A                                      CF07
     A                                      CF08
     A                                      CF09
     A                                      CF10
     A                                      CF11
     A                                      CF12
     A                                      CF13
     A                                      CF14
     A                                      CF15
     A                                      CF16
     A                                      CF17
     A                                      CF18
     A                                      CF19
     A                                      CF20
     A                                      CF21
     A                                      CF22
     A                                      CF23
     A                                      CF24
     A                                      RTNCSRLOC(*WINDOW &CSRROW &CSRCOL &-
     A                                      CSRROW2 &CSRCOL2)
     A                                      RTNCSRLOC(&CSRRCD &CSRFLD &CSRPOS)
     A                                      CSRLOC(OUTROW     OUTCOL)
     A                                      OVERLAY
     A                                      SFLCSRRRN(&CSRRRN1)
     A  51                                  SFLDSP
     A N50                                  SFLDSPCTL
     A  50                                  SFLCLR
     A  98                                  SFLEND
     A                                      SFLSIZ(&SFLSIZ)
     A            OUTRRN1        4S 0H      SFLRCDNBR
     A            OUTROW         3S 0H
     A            OUTCOL         3S 0H
     A            CSRRRN1        5S 0H
     A            CSRRCD        10A  H
     A            CSRFLD        10A  H
     A            CSRPOS         4S 0H
     A            CSRROW         3S 0H
     A            CSRROW2        3S 0H
     A            CSRCOL         3S 0H
     A            CSRCOL2        3S 0H
     A            SFLSIZ         5S 0P
     A            SFLMODE        1A  H
     A            SEL1@          1A  P
     A            ACTCDE1@       1A  P
     A            DES1@          1A  P
     A            FNCKEY1@       1A  P
     A                                  1  1DATE
     A                                      EDTWRD('0  /  /  ')
     A            ENVIRONMNT    40A  O  1 10MSGID(AAA 0000 ENVIRONMNT)
     A            PGMNME2       10A  O  1 51
     A                                  2  1'Select'
     A            TITLE         40A  O  2 10DSPATR(HI)
     A                                  2 51USER
     A            OPTIONS       60A  O  3  1COLOR(BLU)
     A                                  4  1'Search:'
     A            SCHVAL    R        B  4  9
     A                                  5  1'Sel'
     A                                      DSPATR(UL)
     A                                      COLOR(WHT)
     A                                  5  5'Code'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 10'Description                       -
     A                                            '
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A                                  5 51'Funct. Key'
     A                                      DSPATR(HI)
     A                                      DSPATR(UL)
     A            SEL1      R        B  6  2REFFLD(OPTION)
     A                                      DSPATR(&SEL1@)
     A            ACTCDE1   R    2   B  6  5REFFLD(ACTCDE)
     A                                      DSPATR(&ACTCDE1@)
     A            DES1      R        B  6 10REFFLD(DES)
     A                                      DSPATR(&DES1@)
     A                                      CHECK(LC)
     A            FNCKEY1   R   10   B  6 51REFFLD(FNCKEY)
     A                                      DSPATR(&FNCKEY1@)
     A          R FOOTER
     A                                      WINDOW(*DFT 18 60 *NOMSGLIN)
     A                                      OVERLAY
     A            FNCKEYS       60A  O 17  1COLOR(BLU)
     A          R MSGRCD                    TEXT('MSG SFL RECORD')
     A                                      SFL SFLMSGRCD(18)
     A            MSGKEY                    SFLMSGKEY
     A            CALLSTK                   SFLPGMQ
     A          R MSGCTL                    TEXT('MSG SFL CONTROL')
     A                                      OVERLAY SFLCTL(MSGRCD) SFLSIZ(10)
     A                                      SFLPAG(1) SFLDSPCTL SFLDSP SFLINZ
     A N99                                  SFLEND
     A                                      WINDOW(FOOTER)
     A                                      PUTOVR
     A            CALLSTK                   SFLPGMQ
     A          R DUMMY
     A                                      ASSUME
     A            WSFILL         2A  O  2  2
