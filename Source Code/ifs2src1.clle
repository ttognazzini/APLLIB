PGM (&IFSPATH &FILELIB &MBR &MBROPT &TEXT)

/* PARAMETERS */
   DCL &IFSPATH  *CHAR 150
   DCL &FILELIB  *CHAR 20
   DCL   &FILE   *CHAR STG(*DEFINED) LEN(10) DEFVAR(&FILELIB  1)
   DCL   &LIB    *CHAR STG(*DEFINED) LEN(10) DEFVAR(&FILELIB 11)
   DCL &MBR      *CHAR 10
   DCL &MBROPT   *CHAR 10
   DCL &TEXT     *CHAR 50

/* WORK VARIABLES */
   DCL &TOMBR    *CHAR 150
   DCL &PASSPATH *CHAR 151
   DCL &SRCTYPE  *CHAR 10
   DCL &SRCTEXT  *CHAR 50

/* ERROR HANDLING VARIABLES */
   DCL &MSGID    *CHAR 7
   DCL &MSGDTA   *CHAR 256
   DCL &MSGF     *CHAR 10
   DCL &MSGFLIB  *CHAR 10
   DCL &RTNTYPE  *CHAR 2

   MONMSG CPF0000 EXEC(GOTO STDERR)

   /* MAKE SURE THE SOURCE LIBRARY AND FILE EXISTS */
   CHKOBJ     OBJ(&LIB) OBJTYPE(*LIB)
   MONMSG     CPF0000 EXEC(GOTO STDERR)

   /* BUILD THE FULL PATH FOR THE FROM MEMEBR */
   CHGVAR &TOMBR ('/QSYS.lib/' *TCAT &LIB *TCAT '.lib/' *TCAT &FILE *TCAT +
                    '.file/' *TCAT &mbr *tcat '.mbr')

   CPYFRMSTMF FROMSTMF(&IFSPATH) TOMBR(&TOMBR) MBROPT(&MBROPT)

   /* ATTEMP TO GET THE SOURCE TYPE FROM THE FILE EXTENSION */
   CHGVAR &PASSPATH (&IFSPATH *CAT '*')
   CALL IFS2SRB1 (&PASSPATH &SRCTYPE)
   IF (&SRCTYPE *NE ' ') ( +
      CHGPFM FILE(&LIB/&FILE) MBR(&MBR) SRCTYPE(&SRCTYPE) )

   /* SET THE NEW MEMBER TEXT                            +
      IF NOT PASSED, ATTEMP TO GET IT FROM THE FILE TEXT */
   IF (&TEXT *NE ' ') ( +
      CHGPFM  FILE(&LIB/&FILE) MBR(&MBR) TEXT(&TEXT))
   ELSE (DO)
      RTVIFSA IFSFILE(&IFSPATH) TEXT(&SRCTEXT)
      IF (&SRCTEXT *NE ' ') ( +
         CHGPFM  FILE(&LIB/&FILE) MBR(&MBR) TEXT(&SRCTEXT))
   ENDDO

   RETURN

/* STANDARD ERROR HANDLING */
STDERR:
  RCVMSG     MSGTYPE(*LAST) MSGDTA(&MSGDTA) MSGID(&MSGID) +
                 RTNTYPE(&RTNTYPE) +
                 MSGF(&MSGF) SNDMSGFLIB(&MSGFLIB)
  SNDPGMMSG  MSGID(&MSGID)  MSGF(&MSGF)   MSGTYPE(*DIAG) +
                MSGDTA(&MSGDTA)

ENDPGM
